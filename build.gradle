import pl.btwarog.AppConfig
import pl.btwarog.AppDependencies

buildscript {
	repositories {
		google()
		jcenter()
	}
	dependencies {
		classpath AppDependencies.tools_gradle_android
		classpath AppDependencies.tools_kotlin
		classpath AppDependencies.apollo_gradle_plugin
	}
}

allprojects {
	repositories {
		google()
		jcenter()
	}
}

subprojects {
	afterEvaluate { project ->
		if (project.hasProperty('android')) {
			android {
				compileSdkVersion AppConfig.compileSdk
				buildToolsVersion AppConfig.buildToolsVersion

				defaultConfig {
					minSdkVersion AppConfig.minSdk
					targetSdkVersion AppConfig.targetSdk

					testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
				}
				compileOptions {
					sourceCompatibility AppConfig.javaVersion
					targetCompatibility AppConfig.javaVersion
				}
				kotlinOptions {
					jvmTarget = AppConfig.jvmTarget
					freeCompilerArgs += [
							"-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
					]
				}
			}
		} else {
			sourceCompatibility = AppConfig.javaVersion
			targetCompatibility = AppConfig.javaVersion

			compileKotlin {
				kotlinOptions {
					jvmTarget = AppConfig.jvmTarget
					freeCompilerArgs += [
							"-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
					]
				}
			}
			compileTestKotlin {
				kotlinOptions {
					jvmTarget = AppConfig.jvmTarget
					freeCompilerArgs += [
							"-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
					]
				}
			}
		}
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}

apply from: 'gradleScripts/jacoco.gradle'