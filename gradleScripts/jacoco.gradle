import pl.btwarog.AppDependencies

def coveredProject = subprojects

apply plugin: 'jacoco'

configure(coveredProject) { prj ->
	apply plugin: 'jacoco'

	jacoco {
		toolVersion = AppDependencies.jacoco_version
	}

	tasks.withType(Test) {
		jacoco.includeNoLocationClasses = true
	}
	def testTask = prj.projectDir.name == 'core' ? 'test' : 'testDebugUnitTest'
	task jacocoReport(type: JacocoReport, dependsOn: testTask) {
		group = 'Reporting'
		description = 'Generate Jacoco coverage'

		reports {
			csv.enabled = true
			xml.enabled = false
			html.enabled = true
		}

		final fileFilter =
				['**/R.class',
				 '**/R$*.class',
				 '**/BuildConfig.*',
				 '**/Manifest*.*',
				 'android/**/*.*',
				 '**/*Activity*.*',
				 '**/*Screen*.*',
				 '**/*Component*.*',
				 '**/*Module*.*',
				 '**/*Test*.*',
				 '**/*Binding.*',
				 '**/*_MembersInjector.class',
				 '**/*FragmentModule.class',
				 '**/Dagger*Component*.class',
				 '**/*Module_*Factory.class',
				 '**/*_Factory_Impl.*',
				 '**/*_Factory.*',
				 '**/BrowseArtistsQuery*.*',
				 '**/fragment/*.*']
		final kotlinTree = fileTree(dir: "${prj.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
		final javacTree = fileTree(dir: "${prj.buildDir}/intermediates/javac/debug", excludes: fileFilter)
		final mainSrc = "${prj.projectDir}/src/main/java"

		sourceDirectories.setFrom files([mainSrc])
		classDirectories.setFrom files([kotlinTree, javacTree])
		executionData.setFrom fileTree(dir: prj.buildDir, includes: [
				"jacoco/${testTask}.exec", 'outputs/code-coverage/connected/*coverage.ec'
		])
	}
}

task jacocoFullReport(type: JacocoReport, group: 'Coverage reports') {
	group = 'Reporting'
	description = 'Generates an aggregate report from all subprojects'

	tasks.withType(Test) {
		ignoreFailures true
	}

	def projects = coveredProject

	//noinspection GrUnresolvedAccess
	dependsOn(projects.jacocoReport)

	final source = files(projects.jacocoReport.sourceDirectories)

	additionalSourceDirs.setFrom source
	sourceDirectories.setFrom source

	classDirectories.setFrom files(projects.jacocoReport.classDirectories)
	executionData.setFrom files(projects.jacocoReport.executionData)

	reports {
		html {
			enabled true
			destination file('build/reports/jacoco/html')
		}
		csv {
			enabled true
			destination file('build/reports/jacoco/jacocoFullReport.csv')
		}
	}

	doFirst {
		//noinspection GroovyAssignabilityCheck
		executionData.setFrom files(executionData.findAll { it.exists() })
	}
}